// Each #kernel tells which function to compile; you can have many kernels
#pragma editor_sync_compilation
#define THREAD_GROUP_SIZE 128

#define EPSILON 0.000001

#define DT 0.0001

#define WAVE_SPEED 0.5
#define WAVE_DAMPING 0.1
// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
StructuredBuffer<float3> _inputMesh;
RWStructuredBuffer<float> _waveHeight;
RWStructuredBuffer<float> _waveVerticalSpeed;

RWStructuredBuffer<float3> _outputMesh;
StructuredBuffer<int> _inputVertexNeighbours;
StructuredBuffer<int> _inputValence;
int _inputMaxValence;

float cotan_angle(float3 pi, float3 pj, float3 pk)
{
    float3 a = normalize(pi - pk);
    float3 b = normalize(pj - pk);
    float x = tan(acos(min(1.0, max(-1.0, dot(a, b)))));
    return 1 / (x + EPSILON);
}

int mod(int x, int m) {
    return (x % m + m) % m;
}

#pragma kernel CSWaveEquation
[numthreads(THREAD_GROUP_SIZE,1,1)]
void CSWaveEquation (uint3 id : SV_DispatchThreadID)
{
    uint uid = id.x;
    float3 color = float3(0,0,0);
    int valence = _inputValence[uid];
    float3 pi = _inputMesh[uid];
    float weights = 0;
    float area = 0;
    float heightLap = 0;
    float waveVerticalSpeed = 0;
    for (int i = 0; i < valence; i++)
    {
        int neighbor = _inputVertexNeighbours[uid*_inputMaxValence+i];
        float3 pj = _inputMesh[neighbor];
        float3 ka = _inputMesh[_inputVertexNeighbours[uid*_inputMaxValence+mod((i-1),valence)]];
        float3 kb = _inputMesh[_inputVertexNeighbours[uid*_inputMaxValence+mod((i+1),valence)]];
        float neighborWaveHeight = _waveHeight[neighbor];
        float neighborWaveVerticalSpeed = _waveVerticalSpeed[neighbor];

        float a = cotan_angle(pi, pj, ka);
        float b = cotan_angle(pi, pj, kb);
        float ab = a + b;
        weights += ab;

        heightLap += ab*neighborWaveHeight;
        float3 ij = pj - pi;
        float3 ik = ka - pi;
        float ar = length(cross(ij, ik));
        float angle = asin(ar / (length(ij) * length(ik) + EPSILON));
        area += ar*angle; // ar/3.0;
    }
    
    heightLap -= weights*_waveHeight[uid];
    heightLap /= area;
    _waveVerticalSpeed[uid] = _waveVerticalSpeed[uid]*WAVE_DAMPING + DT*(WAVE_SPEED*WAVE_SPEED*heightLap); 
    _waveHeight[uid] = _waveHeight[uid] + DT*_waveVerticalSpeed[uid];
}
